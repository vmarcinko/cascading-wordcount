buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.9.0-M1'
    }
}

apply plugin: 'shadow'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

archivesBaseName = 'cascading-wordcount'
group = 'vmarcinko'
version = '1.0'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

ext {
    slf4jVersion = '1.7.5'
    hadoopVersion = '2.0.0-cdh4.6.0'
    cascadingVersion = '2.5.4'
}

repositories {
	maven {url "https://repository.cloudera.com/artifactory/cloudera-repos"}
	mavenCentral()
	maven{ url 'http://conjars.org/repo/' }
}

configurations {
    hadoopProvided 
}

sourceSets {
    main {
	    compileClasspath += configurations.hadoopProvided
    }
}

idea {
  module {
    scopes.COMPILE.plus += configurations.hadoopProvided
  }
}
eclipse {
    classpath{
        plusConfigurations += configurations.hadoopProvided
    }
}

dependencies {
    compile (
        "cascading:cascading-core:${cascadingVersion}",
        "cascading:cascading-local:${cascadingVersion}",
        "cascading:cascading-hadoop2-mr1:${cascadingVersion}"
    )

    hadoopProvided(
            "org.apache.hadoop:hadoop-client:${hadoopVersion}",
            "commons-httpclient:commons-httpclient:3.1",
            "org.slf4j:slf4j-api:${slf4jVersion}",
            "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
            "org.slf4j:log4j-over-slf4j:${slf4jVersion}",
            "ch.qos.logback:logback-classic:1.0.+"
    )
}
